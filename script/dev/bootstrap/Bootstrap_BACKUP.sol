// SPDX-License-Identifier: MIT
pragma solidity ^0.8.30;

import {Script} from "forge-std/Script.sol";
import {Config} from "forge-std/Config.sol";
import {console} from "forge-std/console.sol";

// local
import {BaseDevScript} from "dev/BaseDevScript.s.sol";
import {OrderEngine} from "orderbook/OrderEngine.sol";
import {DMrktGremlin as DNFT} from "shared/nfts/DMrktGremlin.ERC721.sol";

// interfaces
import {IERC721} from "@openzeppelin/interfaces/IERC721.sol";
import {IERC20} from "@openzeppelin/interfaces/IERC20.sol";

import {IWETH} from "shared/interfaces/IWETH.sol";

interface IMintable721 is IERC721 {
    function mint(address to) external;
}

// NOTE:
// This script is typically executed using the funder private key.
// Explicit vm.startBroadcast(funderPK) calls are used for clarity
// and to guarantee correct sender behavior even if the script
// is invoked from a different context.
contract Bootstrap is BaseDevScript, Config {
    OrderEngine public orderEngine;

    function run() external {
        // --------------------------------
        // PHASE 0: LOAD CONFIG
        // --------------------------------
        _loadConfig("deployments.toml", true);

        logSection("CONFIG & CONTRACT DEPLOYMENT");

        uint256 chainId = block.chainid;

        console.log("ChainId: %s", chainId);

        address funder = config.get("funder").toAddress();
        address weth = config.get("weth").toAddress();

        // --------------------------------
        // PHASE 1: SETUP CONTRACTS
        // --------------------------------
        uint256 funderPK = uint256(uint256(vm.envUint("PRIVATE_KEY")));

        // since the script uses the same private key its not necessary but I like to be explicit
        // deploy orderEngine nft and marketplace
        vm.startBroadcast(funderPK);
        orderEngine = new OrderEngine();
        DNFT dNft = new DNFT();
        vm.stopBroadcast();

        logDeployment("OrderEngine", address(orderEngine));
        logDeployment("DNFT", address(dNft));

        // ---  write deployed addrs to .toml ---
        config.set("verifying_contract", address(orderEngine)); // for our order builder
        config.set("dnft_erc721", address(dNft));

        // --------------------------------
        // PHASE 2: FUND ETH
        // --------------------------------
        logSection("BOOTSTRAP DEV ACCOUNTS");
        console.log("------------------------------------");
        console.log("FUNDER");
        console.log("ADDR  | %s", funder);
        console.log("BAL   | %s", funder.balance);
        console.log("------------------------------------");

        uint256 distributableEth = (funder.balance * 4) / 5;

        // --- PKs for broadcasting ---

        // TODO: replace dev PKs with mnemonic-derived keys
        // https://getfoundry.sh/reference/cheatcodes/derive-key
        uint256[] memory participantPKs = readKeys(chainId);
        uint256 participantCount = participantPKs.length;

        // amount to fund each account
        uint256 bootstrapEth = distributableEth / participantCount;

        vm.startBroadcast(funderPK);

        for (uint256 i = 0; i < participantCount; i++) {
            address a = resolveAddr(participantPKs[i]);

            logBalance("PRE ", a);

            (bool ok, ) = payable(a).call{value: bootstrapEth}("");

            if (!ok) {
                console.log("TRANSFER FAILED -> %s", a);
            } else {
                logBalance("POST", a);
            }

            logSeparator();
        }

        vm.stopBroadcast();

        // --------------------------------
        // PHASE 3: WRAP ETH
        // --------------------------------
        logSection("WRAP ETH => WETH");

        uint256 wethWrapAmount = bootstrapEth / 2;
        IWETH wethToken = IWETH(weth);

        for (uint256 i = 1; i < participantCount; i++) {
            address a = resolveAddr(participantPKs[i]);
            logTokenBalance("PRE  WETH", a, wethToken.balanceOf(a));

            vm.startBroadcast(participantPKs[i]);
            wethToken.deposit{value: wethWrapAmount}();
            vm.stopBroadcast();

            logTokenBalance("POST WETH", a, wethToken.balanceOf(a));

            logSeparator();
        }

        // --------------------------------
        // PHASE 4: MINT NFTs
        // --------------------------------
        logSection("MINT NFTs");

        IMintable721 nftToken = IMintable721(address(dNft));
        uint256 supply = dNft.MAX_SUPPLY();

        // script mints all tokens => limit = MAX_SUPPLY()
        mintTokens(participantPKs, nftToken, supply);

        logSection("DNFT FINAL BALANCES");

        for (uint256 i = 0; i < participantCount; i++) {
            address user = resolveAddr(participantPKs[i]);
            uint256 bal = nftToken.balanceOf(user);

            logTokenBalance("DNFT", user, bal);
        }

        // --------------------------------
        // PHASE 5: APPROVALS
        // --------------------------------
        logSection("APPROVE MARKETPLACE FOR NFTs");

        // WILL OPTIMIZE AND MOVE APPROVAL LOGIC TO Approve.s.sol

        address marketplace = address(orderEngine);

        for (uint256 i = 0; i < participantCount; i++) {
            vm.startBroadcast(participantPKs[i]);
            nftToken.setApprovalForAll(marketplace, true);
            vm.stopBroadcast();

            address owner = resolveAddr(participantPKs[i]);
            console.log(
                "%s HAS APPROVED DMRKT FOR ALL: ",
                owner,
                nftToken.isApprovedForAll(owner, marketplace)
            );
        }

        logSection("APPROVE WETH ALLOWANCE FOR MARKETPLACE");

        // DEV-ONLY:
        // Infinite approval used ONLY for local fork / deterministic setup.
        // Production flow uses exact per-owner exposure-based allowances.
        uint256 allowance = type(uint256).max;

        for (uint256 i = 0; i < participantCount; i++) {
            vm.startBroadcast(participantPKs[i]);
            wethToken.approve(marketplace, allowance);
            vm.stopBroadcast();

            address owner = resolveAddr(participantPKs[i]);
            console.log(
                "%s HAS APPROVED ALLOWANCE FOR MARKETPLACE: ",
                owner,
                wethToken.allowance(owner, marketplace)
            );
        }
    }

    // for dnft limit = maxSupply
    function mintTokens(
        uint256[] memory pks,
        IMintable721 nft,
        uint256 limit
    ) internal {
        // in this script we know i = tokenid for token minted (no previous mints)
        for (uint256 i = 0; i < limit; i++) {
            bytes32 h = keccak256(abi.encode(address(nft), i));
            uint256 j = uint256(h) % pks.length;

            uint256 pk = pks[j];
            address to = resolveAddr(pk);

            // Broadcast as the recipient â€” respect for history
            vm.startBroadcast(pk);

            nft.mint(to);

            vm.stopBroadcast();
        }
    }
}
